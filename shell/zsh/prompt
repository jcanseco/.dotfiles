#!/usr/bin/env zsh

# Inspired by the robbyrussell and risto themes from ohmyzsh
# Contains a fix to a bug in robbyrussell that occurs when using zsh plugin managers
# (see https://github.com/zplug/zplug/issues/95)

source ~/.dotfiles/shell/helpers

function ssh_prompt() {
    if in_ssh; then
        echo "(ssh) "
    fi
}

function git_prompt() {
    # Skip git_prompt_info when in monorepo even if git_prompt_info would not
    # have printed any output. This is done since git_prompt_info takes too
    # long to execute when in a big monorepo, resulting in a laggy shell
    # experience.
    if ! is_pwd_in_monorepo; then
        git_prompt_info
    fi
}

function monorepo_prompt() {
    if is_pwd_in_monorepo; then
        prefix="%{${fg_bold[blue]}%}(%{${fg_bold[white]}%}" # Similar to ZSH_THEME_GIT_PROMPT_PREFIX
        suffix="%{${fg_bold[blue]}%})%{${reset_color}%} " # Similar to ZSH_THEME_GIT_PROMPT_CLEAN + ZSH_THEME_GIT_PROMPT_SUFFIX
        echo "${prefix}monorepo${suffix}"
    fi
}

function time_prompt() {
    echo "(%D{%I:%M %p}) "
}

ret_status="%(?:%{${fg[green]}%}➜ :%{${fg[red]}%}➜ )"

ZSH_THEME_GIT_PROMPT_PREFIX="%{${fg_bold[blue]}%}(%{${fg_bold[red]}%}"
ZSH_THEME_GIT_PROMPT_DIRTY=" %{${fg_bold[yellow]}%}*%{${fg_bold[blue]}%})"
ZSH_THEME_GIT_PROMPT_CLEAN="%{${fg_bold[blue]}%})"
ZSH_THEME_GIT_PROMPT_SUFFIX="%{${reset_color}%} "

PROMPT='$(ssh_prompt)${ret_status} %{${fg_bold[blue]}%}%2~ $(git_prompt)$(monorepo_prompt)%{${reset_color}%}$(time_prompt)'
